plugins {
    // This is used for running "./gradlew dependencyUpdates" to find
    // available library updates (for use when updating version numbers
    // in this file).
    id "com.github.ben-manes.versions" version "0.20.0"

    // This adds ErrorProne, which warns about source code that looks
    // like it's likely to be a bug, or to lead to future bugs.
    id "net.ltgt.errorprone" version "0.7"

    id 'maven-publish'
    id 'signing'

    // This adds tasks to measure code coverage with JaCoCo.
    id 'jacoco'
}

apply plugin: 'java-library'
apply plugin: 'net.ltgt.errorprone'

repositories {
    jcenter()
}

targetCompatibility = '1.8'
sourceCompatibility = '1.8'

dependencies {
    // Dependencies needed at runtime and compile-time:
    implementation 'com.google.guava:guava:27.0.1-jre'
    implementation 'com.google.auto.value:auto-value-annotations:1.6.3'

    annotationProcessor 'com.google.auto.value:auto-value:1.6.3'

    // Dependencies needed for running tests:
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.+'
    testImplementation "com.google.truth:truth:0.42"
    testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.1.4'

    // Dependencies needed when running ErrorProne checks:
    errorprone "com.google.errorprone:error_prone_core:2.3.2"
}

test {
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    jacoco {
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
    }
}

jacoco {
    toolVersion = "0.8.2"
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
                fileTree(dir: it,
                         exclude: ['**/*AutoValue_*'])
            })
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            limit {
                minimum = 0.95
            }
            excludes = [
                "*.AutoValue_*"
            ]
        }
    }
}
check.dependsOn jacocoTestCoverageVerification

// tell the ErrorProne compiler not to warn about issues in generated
// code (e.g. AutoValue generated classes)
tasks.withType(JavaCompile).configureEach {
  options.errorprone.disableWarningsInGeneratedCode = true
}


// Generate Javadoc documentation files from Java source code.
javadoc {
    title = 'time-utils API'
    source = sourceSets.main.allJava
    // This classpath value makes the source's dependencies visible to
    // the Javadoc tool.
    classpath = sourceSets.main.runtimeClasspath

    options.addBooleanOption('html5', true)
    options {
        // Only document public members.
        setMemberLevel JavadocMemberLevel.PUBLIC
        setAuthor false

        // Generate Javadocs links even when offline, using the
        // element-list or package-list file copied from the public
        // Javadocs to a local subdir. (The package-list file contains
        // a list of Java package names of classes which are
        // documented in the Javadocs rooted at the specified URL.)
        linksOffline "https://docs.oracle.com/javase/10/docs/api/", 'gradle/javadocs/jdk/10'
        linksOffline "https://google.github.io/guava/releases/27.0.1-jre/api/docs/", 'gradle/javadocs/guava/27.0.1-jre'
    }
}

// Make a source code JAR archive.
task sourcesJar(type: Jar, dependsOn: 'classes') {
    classifier = 'sources'
    from sourceSets.main.allJava
}

// Make a Javadoc JAR archive.
task javadocJar(type: Jar, dependsOn: 'javadoc') {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            // Include the sources and Javadoc JAR files when
            // publishing.
            artifact sourcesJar
            artifact javadocJar

            afterEvaluate {
                groupId = 'com.pervasivecode'
                artifactId = 'time-utils'

                // This must end in "-SNAPSHOT" for publishing assets
                // to a "snapshot" Maven repository to work, or else
                // the Gradle task will fail with a 400 Bad Request
                // response. Likewise, publishing assets to a release
                // staging repository will only work if this version
                // does _not_ end in "-SNAPSHOT".
                version = '1.0-SNAPSHOT'

                pom {
                    name = 'Java Time Utilities'
                    description = 'Classes to to format Durations, and simplify writing easily-testable code that uses real-world time (delays, stopwatches, etc.)'
                    url = 'https://github.com/JamieFlournoy/java-time-utils'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'jamieflournoy'
                            name = 'Jamie Flournoy'
                            organization = 'Pervasive Code'
                            email = 'jamie@pervasivecode.com'
                            url = 'https://github.com/JamieFlournoy'
                            organizationUrl = 'http://www.pervasivecode.com/'
                        }
                    }

                    issueManagement {
                        url = 'https://github.com/JamieFlournoy/java-time-utils/issues'
                        system = 'GitHub Issues'
                    }

                    scm {
		        url = 'https://github.com/JamieFlournoy/java-time-utils'
                        connection = 'scm:git:git://github.com/JamieFlournoy/java-time-utils.git'
		        developerConnection = 'scm:git:git@github.com:JamieFlournoy/java-time-utils.git'
                    }
                }

            }
        }
    }

    // Publish directly to a local directory (not the local Maven
    // repository, but a directory under the build dir), for
    // inspection & manual uploading.
    repositories {
        maven {
            name = 'buildDir'
            url = "file://${buildDir}/repo"
        }
        // Note: to deploy to this repository, you must set the
        // OSSRH_PASSWORD environment variable. Also, the publication
        // version (see above) must end in "-SNAPSHOT" or this will
        // fail with an HTTP response code of 400 Bad Request.
        maven {
            name = 'ossrhSnapshots'
            url = 'https://oss.sonatype.org/content/repositories/snapshots'
            credentials {
                username = ossrhUsername
                password = System.getenv('OSSRH_PASSWORD')
            }
        }
        // Note: to deploy to this repository, you must set the
        // OSSRH_PASSWORD environment variable. Also, the publication
        // version (see above) must _not_ end in "-SNAPSHOT" or this
        // will fail with an HTTP response code of 400 Bad Request.
        maven {
            name = 'ossrhReleaseStaging'
            url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            credentials {
                username = ossrhUsername
                password = System.getenv('OSSRH_PASSWORD')
            }
        }
    }
}

signing {
    // Sign assets that are destined for publication in a Maven Java
    // archive.
    useGpgCmd()
    sign publishing.publications.mavenJava
}
