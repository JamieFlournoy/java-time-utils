plugins {
    // This is used for running "./gradlew dependencyUpdates" to find
    // available library updates (for use when updating version numbers
    // in this file).
    id "com.github.ben-manes.versions" version "0.20.0"

    // This adds ErrorProne, which warns about source code that looks
    // like it's likely to be a bug, or to lead to future bugs.
    id "net.ltgt.errorprone" version "0.6"

    id 'maven-publish'
    id 'signing'
}

apply plugin: 'java-library'
apply plugin: 'net.ltgt.errorprone'

repositories {
    jcenter()
}

def errorprone_core_lib = "com.google.errorprone:error_prone_core:2.3.1"

dependencies {
    // Dependencies needed at runtime and compile-time:
    implementation 'com.google.guava:guava:26.0-jre'

    // Dependencies needed only for running tests:
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.+'
    testImplementation "com.google.truth:truth:0.30"

    // Dependencies needed when running ErrorProne checks:
    errorprone errorprone_core_lib
}

// Make a source code JAR archive.
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

// Make a Javadoc JAR archive.
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            // Include the sources and Javadoc JAR files when
            // publishing.
            artifact sourcesJar
            artifact javadocJar

            afterEvaluate {
                groupId = 'com.pervasivecode'
                artifactId = 'java-time-utils'
                version = '0.9'

                pom {
                    name = 'Java Time Utilities'
                    description = 'Classes to simplify writing easily-testable code that uses real-world time (delays, stopwatches, etc.)'
                    url = 'https://github.com/JamieFlournoy/java-time-utils'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'jamieflournoy'
                            name = 'Jamie Flournoy'
                            organization = 'Pervasive Code'
                            email = 'jamie@pervasivecode.com'
                            url = 'https://github.com/JamieFlournoy'
                            organizationUrl = 'http://www.pervasivecode.com/'
                        }
                    }

                    issueManagement {
                        url = 'https://github.com/JamieFlournoy/java-time-utils/issues'
                        system = 'GitHub Issues'
                    }

                    scm {
		        url = 'https://github.com/JamieFlournoy/java-time-utils'
                        connection = 'scm:git:git://github.com/JamieFlournoy/java-time-utils.git'
		        developerConnection = 'scm:git:git@github.com:JamieFlournoy/java-time-utils.git'
                    }
                }

            }
        }
    }

    // Publish directly to a local directory (not the local Maven
    // repository, but a directory under the build dir), for
    // inspection & manual uploading.
    repositories {
        maven {
            name = 'buildRepo'
            url = "file://${buildDir}/repo"
        }
    }
}

signing {
    // Sign assets that are destined for publication in a Maven Java
    // archive.
    useGpgCmd()
    sign publishing.publications.mavenJava
}
